

// Â© luis12614

//@version=3
//2021-present, Luis Roman (https://about.me/luis-roman)

study("BuySell", overlay=true)

length = input(title="ATR Period", type=integer, defval=22)
mult = input(title="ATR Multiplier", type=float, step=0.1, defval=3.0)
showLabels = input(title="Show Buy/Sell Labels ?", type=bool, defval=true)

atr = mult * atr(length)

longStop = hl2 - atr
longStopPrev = nz(longStop[1], longStop)
longStop := close[1] > longStopPrev ? max(longStop, longStopPrev) : longStop

shortStop = hl2 + atr
shortStopPrev = nz(shortStop[1], shortStop)
shortStop := close[1] < shortStopPrev ? min(shortStop, shortStopPrev) : shortStop

dir = 1
dir := nz(dir[1], dir)
dir := dir == -1 and close > shortStopPrev ? 1 : dir == 1 and close < longStopPrev ? -1 : dir

longColor = green
shortColor = red

plot(dir == 1 ? longStop : na, title="Long Stop", style=linebr, linewidth=2, color=longColor)
buySignal = dir == 1 and dir[1] == -1
plotshape(buySignal ? longStop : na, title="Long Stop Start", location=location.absolute, style=shape.circle, size=size.tiny, color=longColor, transp=0)
plotshape(buySignal and showLabels ? longStop : na, title="Buy Label", text="Buy", location=location.absolute, style=shape.labelup, size=size.tiny, color=longColor, textcolor=white, transp=0)

plot(dir == 1 ? na : shortStop, title="Short Stop", style=linebr, linewidth=2, color=shortColor)
sellSignal = dir == -1 and dir[1] == 1
plotshape(sellSignal ? shortStop : na, title="Short Stop Start", location=location.absolute, style=shape.circle, size=size.tiny, color=shortColor, transp=0)
plotshape(sellSignal and showLabels ? shortStop : na, title="Sell Label", text="Sell", location=location.absolute, style=shape.labeldown, size=size.tiny, color=shortColor, textcolor=white, transp=0)

alertcondition(buySignal, title="Alert on SuperTrend Buy", message="SuperTrend Buy!")
alertcondition(sellSignal, title="Alert on SuperTrend Sell", message="SuperTrend Sell!")

changeCond = dir != dir[1]
alertcondition(changeCond, title="Alert on SuperTrend Direction Change", message="SuperTrend has changed direction!")
